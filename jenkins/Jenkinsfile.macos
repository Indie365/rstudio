def utils

pipeline {

  agent { label 'macos-v1.4-arm64' }

  options {
    // timeout regardless of activity
    timeout(activity: false, time: 120)
    timestamps()
    disableConcurrentBuilds()
    buildDiscarder(logRotator(numToKeepStr: '100'))
  }

  parameters {
      string(name: 'SLACK_CHANNEL', defaultValue: '#ide-builds', description: 'Slack channel to publish build message.')
      string(name: 'RSTUDIO_VERSION_PATCH',  defaultValue: '999', description: 'RStudio Patch Version')
      booleanParam(name: 'DAILY', defaultValue: false, description: 'Runs daily build if true')
      booleanParam(name: 'PUBLISH', defaultValue: false, description: 'Runs publish stage if true')
      string(name: 'COMMIT_HASH', defaultValue: '')
  }

  environment {
    PATH = "$HOME/opt/bin:/usr/local/bin:/usr/bin:/bin:/usr/sbin:/sbin"
    PACKAGE_OS = 'macOS'
    RSTUDIO_VERSION = ""
    RSTUDIO_VERSION_MAJOR = 0
    RSTUDIO_VERSION_MINOR = 0
    RSTUDIO_VERSION_PATCH = 0
    RSTUDIO_VERSION_SUFFIX = 0
    IS_PRO = false
  }
  stages {

    stage('Load Utils') {
      steps {
        script {
          utils = load "${env.WORKSPACE}/utils.groovy"
        }
      }
    }

    stage ("Checkout") {
      when { expression { params.COMMIT_HASH != '' } }

      steps {
        echo "Commit_hash value: ${params.COMMIT_HASH}"
        checkout([$class: 'GitSCM',
                  branches: [[name: "${params.COMMIT_HASH}"]],
                  extensions: [],
                  userRemoteConfigs: [[credentialsId: 'github-rstudio-jenkins', url: "${GIT_URL}"]]])
      }
    }

    stage('Versioning') {
      steps {
        script {
          (RSTUDIO_VERSION,
            RSTUDIO_VERSION_MAJOR,
            RSTUDIO_VERSION_MINOR,
            RSTUDIO_VERSION_PATCH,
            RSTUDIO_VERSION_SUFFIX) = utils.getVersion()
          IS_PRO = RSTUDIO_VERSION_SUFFIX.contains('pro')
        }

        buildName "${RSTUDIO_VERSION_MAJOR}.${RSTUDIO_VERSION_MINOR}.${RSTUDIO_VERSION_PATCH}${RSTUDIO_VERSION_SUFFIX}"
      }
    }

    stage('Install Dependencies') {
      environment {
        // boost won't compile without the brew version of openssl.
        // only add it to the dep resolve step though, or the ide build will compile against the wrong openssl
        PATH = '/usr/local/opt/openssl/bin:/usr/local/opt/openssl/bin:/usr/local/bin:/usr/bin:/bin:/usr/sbin:/sbin'
        GITHUB_LOGIN = credentials('github-rstudio-jenkins')
      }
      steps {
        sh 'cd dependencies/osx && RSTUDIO_GITHUB_LOGIN=$GITHUB_LOGIN ./install-dependencies-osx && cd ../..'
      }
    }

    stage("Build Matrix") {

      environment {
        ENV = "RSTUDIO_VERSION_MAJOR=${RSTUDIO_VERSION_MAJOR} RSTUDIO_VERSION_MINOR=${RSTUDIO_VERSION_MINOR} RSTUDIO_VERSION_PATCH=${RSTUDIO_VERSION_PATCH} RSTUDIO_VERSION_SUFFIX=${RSTUDIO_VERSION_SUFFIX}"
      }

      matrix {

        axes {
          axis {
            name 'FLAVOR'
            values 'Electron', 'Desktop' // desktop denotes a Qt build
          }
        }

        when {
          anyOf {
            expression { return FLAVOR == 'Electron' }
            expression { return FLAVOR == 'Desktop' && IS_PRO == true } // Only build Qt on Pro
          }
        }

        stages {
          stage('Sequential Matrix') {
            options {
              lock('synchronous-matrix')
            }
            stages{
              stage('Build and Sign') {

                environment {
                  AWS_ACCOUNT_ID = '749683154838'
                  KEYCHAIN_PASSPHRASE = credentials('ide-keychain-passphrase')
                }

                steps {
                  script {
                    // Set sccache enabled
                    if (params.DAILY == false) {
                      ENV = "${ENV} SCCACHE_ENABLED=1"
                    }
                  }
                  // unlock keychain to ensure build gets signed.
                  sh 'security unlock-keychain -p ${KEYCHAIN_PASSPHRASE} && security set-keychain-settings' // turn off timeout
                  // build rstudio
                  dir ("package/osx") {
                    withAWS(role: 'build', roleAccount: AWS_ACCOUNT_ID) {
                      sh "${ENV} ./make-package --rstudio-target=${FLAVOR}"
                    }
                  }
                }
              }

              stage('Test') {
                environment {
                  PATH = "${env.HOME}/opt/bin:${env.PATH}"
                }
                steps {
                  dir("package/osx/build/src/cpp") {
                    // attempt to run cpp unit tests
                    // problems with rsession finding openssl, so those tests
                    // are disabled until we solve it (#6890)
                    sh "arch -x86_64 ./rstudio-tests"
                    
                    // electron tests not working in CI environment, under investigation
                    // if(FLAVOR == 'Electron') {
                    //   sh "cd ../../../../../src/node/desktop/ && $HOME/.yarn/bin/yarn && $HOME/.yarn/bin/yarn test"
                    // }
                    }
                  }
                }

              stage('Notarize and Upload') {
                when { expression { return params.PUBLISH } }
                
                environment {
                  PATH = "${env.HOME}/opt/bin:${env.PATH}"
                  PACKAGE_FILE = ""
                  BUILD_TYPE = ""
                  PRODUCT = "${FLAVOR.toLowerCase()}"
                }
                
                stages {
                  stage("Set Release Variables") {
                    steps {
                      script {
                        // extract name of package to publish
                        PACKAGE_FILE = sh (
                          script: "basename `ls package/osx/build/RStudio-*.dmg`",
                          returnStdout: true
                        ).trim()

                        BUILD_TYPE= sh (
                          script: "cat version/BUILDTYPE",
                          returnStdout: true
                        ).trim().toLowerCase()
                        if (IS_PRO) {
                          PRODUCT = PRODUCT + "-pro"
                        }
                      }
                    }
                  }

                  stage("Notarize") {
                    environment {
                      APPLE_ID = credentials('ide-apple-notarizer')
                    }

                    steps {
                      sh "docker/jenkins/notarize-release.sh package/osx/build/${PACKAGE_FILE}"
                    }
                  }

                  stage("Upload Package") {
                    steps {
                      // this job is going to run on a macOS build agent, which cannot use an instance-profile
                      withAWS(role: 'ide-build', region: 'us-east-1') {
                        retry(5) {
                          script {
                            utils.uploadPackageToS3 "package/osx/build/${PACKAGE_FILE}", "${PRODUCT}/macos/"
                          }
                        }
                      }
                    }
                  }

                  stage("Upload Daily Build Redirects") {
                    environment {
                      RSTUDIO_ORG_PEM = credentials('www-rstudio-org-pem')
                    }
                    when { // See https://github.com/rstudio/rstudio-pro/commit/0590beeba8ff898b6c9ed8f524ca734bb830f930
                      expression {
                        return IS_PRO != true && FLAVOR == 'Electron';
                      }
                    }
                    steps {
                      // upload daily build redirects
                      sh 'docker/jenkins/publish-daily-binary.sh https://s3.amazonaws.com/rstudio-ide-build/${PRODUCT}/macos/${PACKAGE_FILE} ${RSTUDIO_ORG_PEM}'
                    }
                  }
                  
                  stage("Sentry Upload") {
                    when { expression { return params.DAILY } }

                    environment {
                      SENTRY_API_KEY = credentials('ide-sentry-api-key')
                    }
                    when {
                      expression {
                        return DAILY;
                      }
                    }
                    steps {
                      // upload debug symbols to Sentry
                      retry(5) {
                        // timeout sentry in 15 minutes
                        timeout activity: true, time: 15 {
                          // upload Javascript source maps
                          dir ('package/osx/build/gwt') {
                            script {
                              utils.sentryUploadSourceMaps
                            }
                          }

                          // upload C++ debug information
                          dir ('package/osx/build/src/cpp') {
                            script {
                              utils.sentryUpload 'dsym'
                            }
                          }
                        }
                      }
                    }
                  }
                  
                  stage("Publish") {
                    environment {
                      GITHUB_LOGIN = credentials('github-rstudio-jenkins')
                    }
                    steps {
                      script {
                        // publish build to dailies page
                        utils.publishToDailiesSite PACKAGE_FILE "${PRODUCT}/macos"
                      }
                    }
                  }
                }
              }
            }
          }
        }
      }
    }
  }

  post {
    always {
      deleteDir()
      sendNotifications slack_channel: SLACK_CHANNEL
    }
  }

}
